set terminal postscript enhanced eps  defaultplex \
   leveldefault  blacktext \
   dashed dashlength 2.0 linewidth 3.0 butt \
   palfuncparam 2000,0.003 \
   "Helvetica" 24 

set grid y lt 0 lw 1 lc rgb "#B8B8B8"
set grid x lt 0 lw 1 lc rgb "#B8B8B8"
set ylabel "CDF"
set xlabel xTitle
set style data lines

set log x
set format x "%g %%"

N = system(sprintf("awk '{print NF; exit}' %s", fromFile))
if (N==1){
	N = system(sprintf("awk -F ';' '{print NF; exit}' %s", fromFile));
}
if (N>2){
	set yrange[0:1]
	unset log x
    set datafile separator ";"
	set key below right
	plot fromFile u 2:1 lt 1 lc rgb 'red' lw 1.5 title "Advertising",\
		'' u 4:3 lt 1 lc rgb 'blue' lw 1.5 title "Analytics",\
		'' u 6:5 lt 1 lc rgb 'green' lw 1.5 title "Social",\
		'' u 10:9 lt 1 lc rgb 'brown' lw 1.5 title "Beacons",\
		'' u 8:7 lt 1 lc rgb 'grey' lw 1.5  title "Content",\
		'' u 12:11 lt 1 lc rgb 'black' lw 1.5 title "Others"
}else{
	set nokey
	plot fromFile u 2:1 lt 1 lc rgb '#0060ad' lw 2.5
}

	

